
import ./entitytags as entityTags
import ./chances as chances
import ./loottables as lootTables
from ./particles import circle

append function_tag load:load {"values":["ps-mob:load"]}
function ./load:
    scoreboard objectives add ps-mob dummy
    scoreboard objectives add ps-mob.config dummy [{"text":"M","color":"white"},{"text":"o","color":"#FF0000"},{"text":"b C","color":"white"},{"text":"a","color":"#FF0000"},{"text":"ptains ","color":"white"},{"text":"Config","color":"gold"}]

    def init_config(name, value):
        unless score name ps-mob.config = name ps-mob.config
            scoreboard players set name ps-mob.config value

    init_config(".cool_down", 60)
    init_config(".spawn_chance", 10)

    init_config(".weight_common", 50)
    init_config(".weight_uncommon", 30)
    init_config(".weight_rare", 9)
    init_config(".weight_legendary", 2)

    init_config(".notify_on_spawn", 2)
    init_config(".notify_with_sound", 1)
    init_config(".should_glow", 0)
    init_config(".beam_on_spawn", 1)

    team add ps-mob.common
    team modify ps-mob.common color gray
    team add ps-mob.uncommon
    team modify ps-mob.uncommon color green
    team add ps-mob.rare
    team modify ps-mob.rare color dark_purple
    team add ps-mob.legendary
    team modify ps-mob.legendary color gold

    unless score #random ps-mob = #random ps-mob store result score #random ps-mob run seed
    unless score #rand_a ps-mob = #rand_a ps-mob scoreboard players set #rand_a ps-mob 1234567
    unless score #rand_c ps-mob = #rand_c ps-mob scoreboard players set #rand_c ps-mob 1234567

    schedule function ./tick 2t replace:
        schedule function ./tick 2t replace
        as @e[type=#ps-mob:captains,tag=ps-mob.captain] at @s run function ./particles/main:
            if entity @s[tag=ps-mob.common] run function ./particles/common:
                circle(1, 15, "dust 1 1 1 1", offset=(0,0.1,0))
                circle(2, 30, "dust 1 1 1 1", offset=(0,0.1,0))
            if entity @s[tag=ps-mob.uncommon] run function ./particles/uncommon:
                circle(1, 15, "dust 0 1 0 1", offset=(0,0.1,0))
                circle(2, 30, "dust 0 1 0 1", offset=(0,0.1,0))
            if entity @s[tag=ps-mob.rare] run function ./particles/rare:
                circle(1, 15, "dust 0.7 0 1 1", offset=(0,0.1,0))
                circle(2, 30, "dust 0.7 0 1 1", offset=(0,0.1,0))
            if entity @s[tag=ps-mob.legendary] run function ./particles/legendary:
                circle(1, 15, "dust 10 10 10 1", offset=(0,0.1,0))
                circle(2, 30, "dust 10 10 10 1", offset=(0,0.1,0))
            anchored eyes positioned ^ ^ ^ run function ./particles/head:
                circle(0.25, 5, "soul_fire_flame", "out", 0.03, movement_offset=(0,2,0), offset=(0,0.8,0))
                circle(0.25, 5, "flame", "out", 0.13, offset=(0,0.8,0))

    schedule function ./register_new 1s replace:
        schedule function ./register_new 1s replace
        if score .cool_down ps-mob matches 1.. scoreboard players remove .cool_down ps-mob 1
        unless score .cool_down ps-mob matches 1.. as @e[type=#ps-mob:captains,tag=!ps-mob,tag=!smithed.entity] function ./captain_check
        tag @e[type=#ps-mob:captains,tag=!ps-mob,tag=!smithed.entity] add ps-mob

function ./captain_check:
    unless score .cool_down ps-mob matches 1.. scoreboard players add .failure ps-mob 1

    scoreboard players reset #try ps-mob
    unless score .cool_down ps-mob matches 1.. if predicate ./should_spawn if predicate ./per_failure scoreboard players set #try ps-mob 1

    scoreboard players operation .weight_max ps-mob = .weight_common ps-mob.config
    scoreboard players operation .weight_max ps-mob += .weight_uncommon ps-mob.config
    scoreboard players operation .weight_max ps-mob += .weight_rare ps-mob.config
    scoreboard players operation .weight_max ps-mob += .weight_legendary ps-mob.config
    function ./next_random
    scoreboard players operation .rand% ps-mob = #random ps-mob
    scoreboard players operation .rand% ps-mob %= .weight_max ps-mob
    scoreboard players add .rand% ps-mob 1

    if score #try ps-mob matches 1
        unless score .cool_down ps-mob matches 1..
        if score .rand% ps-mob <= .weight_common ps-mob.config
        run function ./convert/common:
            attribute @s minecraft:generic.max_health modifier add 15213119-2-2-2-2 ps-mob.max_health 0.5 multiply
            attribute @s minecraft:generic.armor modifier add 15213119-2-2-2-2 ps-mob.armor 2 add
            attribute @s minecraft:generic.movement_speed modifier add 15213119-2-2-2-2 ps-mob.movement_speed 0.15 multiply
            attribute @s minecraft:generic.attack_damage modifier add 15213119-2-2-2-2 ps-mob.attack_damage 0.7 multiply
            attribute @s minecraft:generic.knockback_resistance modifier add 15213119-2-2-2-2 ps-mob.knockback_resistance 0.03 add
            tag @s add ps-mob.common
            team join ps-mob.common @s
            data modify entity @s DeathLootTable set value "ps-mob:captain/common"
            if score .notify_on_spawn ps-mob.config matches 4.. at @s run tellraw @a[distance=..128] ["", {"text": "\\u2620", "color": "gray"}, {"text": " A ", "color": "white"}, {"text": "common Mob Captain", "color": "gray"}, {"text": " has spawned nearby!", "color": "white"}]
            if score .notify_on_spawn ps-mob.config matches 4.. if score .notify_with_sound ps-mob.config matches 1 at @s run playsound entity.wither.spawn ambient @a[distance=..128] ~ ~ ~ 0.5 1.8 0.5
            if score .beam_on_spawn ps-mob.config matches 1 at @s run particle dust 1 1 1 5 ~ ~ ~ 0 200 0 0 300 force
            function ./convert/captain
    if score #try ps-mob matches 1 scoreboard players operation .rand% ps-mob -= .weight_common ps-mob.config

    if score #try ps-mob matches 1
        unless score .cool_down ps-mob matches 1..
        if score .rand% ps-mob <= .weight_uncommon ps-mob.config
        run function ./convert/uncommon:
            attribute @s minecraft:generic.max_health modifier add 15213119-2-2-2-2 ps-mob.max_health 0.9 multiply
            attribute @s minecraft:generic.armor modifier add 15213119-2-2-2-2 ps-mob.armor 4 add
            attribute @s minecraft:generic.movement_speed modifier add 15213119-2-2-2-2 ps-mob.movement_speed 0.25 multiply
            attribute @s minecraft:generic.attack_damage modifier add 15213119-2-2-2-2 ps-mob.attack_damage 1.2 multiply
            attribute @s minecraft:generic.knockback_resistance modifier add 15213119-2-2-2-2 ps-mob.knockback_resistance 0.1 add
            tag @s add ps-mob.uncommon
            team join ps-mob.uncommon @s
            data modify entity @s DeathLootTable set value "ps-mob:captain/uncommon"
            if score .notify_on_spawn ps-mob.config matches 3.. at @s run tellraw @a[distance=..128] ["", {"text": "\\u2620", "color": "#00FF00"}, {"text": " An ", "color": "white"}, {"text": "uncommon Mob Captain", "color": "#00FF00"}, {"text": " has spawned nearby!", "color": "white"}]
            if score .notify_on_spawn ps-mob.config matches 3.. if score .notify_with_sound ps-mob.config matches 1 at @s run playsound entity.wither.spawn ambient @a[distance=..128] ~ ~ ~ 0.5 1.8 0.5
            if score .beam_on_spawn ps-mob.config matches 1 at @s run particle dust 0 1 0 5 ~ ~ ~ 0 200 0 0 300 force
            function ./convert/captain
    if score #try ps-mob matches 1 scoreboard players operation .rand% ps-mob -= .weight_uncommon ps-mob.config

    if score #try ps-mob matches 1
        unless score .cool_down ps-mob matches 1..
        if score .rand% ps-mob <= .weight_rare ps-mob.config
        run function ./convert/rare:
            attribute @s minecraft:generic.max_health modifier add 15213119-2-2-2-2 ps-mob.max_health 1.8 multiply
            attribute @s minecraft:generic.armor modifier add 15213119-2-2-2-2 ps-mob.armor 8 add
            attribute @s minecraft:generic.movement_speed modifier add 15213119-2-2-2-2 ps-mob.movement_speed 0.4 multiply
            attribute @s minecraft:generic.attack_damage modifier add 15213119-2-2-2-2 ps-mob.attack_damage 2.1 multiply
            attribute @s minecraft:generic.knockback_resistance modifier add 15213119-2-2-2-2 ps-mob.knockback_resistance 0.5 add
            tag @s add ps-mob.rare
            team join ps-mob.rare @s
            data modify entity @s DeathLootTable set value "ps-mob:captain/rare"
            if score .notify_on_spawn ps-mob.config matches 2.. at @s run tellraw @a[distance=..128] ["", {"text": "\\u2620", "color": "#B400FF"}, {"text": " A ", "color": "white"}, {"text": "rare Mob Captain", "color": "#B400FF"}, {"text": " has spawned nearby!", "color": "white"}]
            if score .notify_on_spawn ps-mob.config matches 2.. if score .notify_with_sound ps-mob.config matches 1 at @s run playsound entity.wither.spawn ambient @a[distance=..128] ~ ~ ~ 0.5 1.8 0.5
            if score .beam_on_spawn ps-mob.config matches 1 at @s run particle dust 0.7 0 1 5 ~ ~ ~ 0 200 0 0 300 force
            function ./convert/captain
    if score #try ps-mob matches 1 scoreboard players operation .rand% ps-mob -= .weight_rare ps-mob.config

    if score #try ps-mob matches 1
        unless score .cool_down ps-mob matches 1..
        if score .rand% ps-mob <= .weight_common ps-mob.config
        run function ./convert/legendary:
            attribute @s minecraft:generic.max_health modifier add 15213119-2-2-2-2 ps-mob.max_health 2.7 multiply
            attribute @s minecraft:generic.armor modifier add 15213119-2-2-2-2 ps-mob.armor 15 add
            attribute @s minecraft:generic.movement_speed modifier add 15213119-2-2-2-2 ps-mob.movement_speed 0.8 multiply
            attribute @s minecraft:generic.attack_damage modifier add 15213119-2-2-2-2 ps-mob.attack_damage 3 multiply
            attribute @s minecraft:generic.knockback_resistance modifier add 15213119-2-2-2-2 ps-mob.knockback_resistance 1 add
            tag @s add ps-mob.legendary
            team join ps-mob.legendary @s
            data modify entity @s DeathLootTable set value "ps-mob:captain/legendary"
            if score .notify_on_spawn ps-mob.config matches 1.. at @s run tellraw @a[distance=..128] ["", {"text": "\\u2620", "color": "red"}, {"text": " A ", "color": "white"}, {"text": "l", "color": "#ff5151"}, {"text": "e", "color": "#ffb74d"}, {"text": "g", "color": "#ffff87"}, {"text": "e", "color": "#51ff51"}, {"text": "n", "color": "#4d4dff"}, {"text": "d", "color": "#9933cc"}, {"text": "a", "color": "#cc66cc"}, {"text": "r", "color": "#cc00cc"}, {"text": "y", "color": "#ff87ff"}, {"text": " Mob Captain", "color": "red"}, {"text": " has spawned nearby!", "color": "white"}]
            if score .notify_on_spawn ps-mob.config matches 1.. if score .notify_with_sound ps-mob.config matches 1 at @s run playsound entity.wither.spawn ambient @a[distance=..128] ~ ~ ~ 0.5 1.8 0.5
            if score .beam_on_spawn ps-mob.config matches 1 at @s run particle dust 10 10 10 5 ~ ~ ~ 0 200 0 0 300 force
            function ./convert/captain

function ./convert/captain:
    if score .should_glow ps-mob.config matches 1 effect give @s glowing 1000000 0 true
    tag @s add smithed.entity
    tag @s add ps-mob.captain
    scoreboard players reset #try ps-mob
    scoreboard players reset .faliure ps-mob
    scoreboard players operation .cool_down ps-mob = .cool_down ps-mob.config
    store result entity @s Health float 1 attribute @s minecraft:generic.max_health get

function ./next_random:
    scoreboard players operation #random ps-mob *= #rand_a ps-mob
    scoreboard players operation #random ps-mob += #rand_c ps-mob

function ./config:
    tellraw @s [
        "",
        { "text": "\n" },
        {"text":"M","color":"white"},{"text":"o","color":"#FF0000"},{"text":"b C","color":"white"},{"text":"a","color":"#FF0000"},{"text":"ptains ","color":"white"},{"text":"Config","color":"gold"},
        { "text": "\n" },
        { "text": "Cool down ",
            "clickEvent": {
                "action": "suggest_command",
                "value": "/scoreboard players set .cool_down ps-mob.config " },
            "hoverEvent": {
                "action": "show_text",
                "contents": "The time, in seconds, to stop trying to spawn a captain after successfully spawning one" }},
        { "text": "[0 to 2147483647]", "color": "gray" },
        { "text": " " },
        { "score": {
                "name": ".cool_down",
                "objective": "ps-mob.config" },
            "color": "red" },
        { "text": "\n" },
        { "text": "Spawn chance ",
            "clickEvent": {
                "action": "suggest_command",
                "value": "/scoreboard players set .spawn_chance ps-mob.config " },
            "hoverEvent": {
                "action": "show_text",
                "contents": "The maximum chance, that a mob becomes a Mob Captain, with 1 being 0.1% and 1000 being 100%" }},
        { "text": "[0 - 1000]", "color": "gray" },
        { "text": " " },
        { "score": {
                "name": ".spawn_chance",
                "objective": "ps-mob.config" },
            "color": "red" },
        { "text": "\n" },
        { "text": "Notify on spawn ",
            "clickEvent": {
                "action": "suggest_command",
                "value": "/scoreboard players set .notify_on_spawn ps-mob.config " },
            "hoverEvent": {
                "action": "show_text",
                "contents": "What rarity of Mob Captain is announced to chat, with 0 being for none, 1 only for legendaries and 4 being for all rarities of Mob Captain" }},
        { "text": "[0 - 4]", "color": "gray" },
        { "text": " " },
        { "score": {
                "name": ".notify_on_spawn",
                "objective": "ps-mob.config" },
            "color": "red" },
        { "text": "\n" },
        { "text": "Notify with sound ",
            "clickEvent": {
                "action": "suggest_command",
                "value": "/scoreboard players set .notify_with_sound ps-mob.config " },
            "hoverEvent": {
                "action": "show_text",
                "contents": "If a Mob Captain is announced to chat, a sound to all nearby players will play, with 0 being disabled and 1 being enabled" }},
        { "text": "[0 | 1]", "color": "gray" },
        { "text": " " },
        { "score": {
                "name": ".notify_with_sound",
                "objective": "ps-mob.config" },
            "color": "red" },
        { "text": "\n" },
        { "text": "Should glow ",
            "clickEvent": {
                "action": "suggest_command",
                "value": "/scoreboard players set .should_glow ps-mob.config " },
            "hoverEvent": {
                "action": "show_text",
                "contents": "If a Mob Captain's silhouette should show through blocks, with 0 being disabled and 1 being enabled" }},
        { "text": "[0 | 1]", "color": "gray" },
        { "text": " " },
        { "score": {
                "name": ".should_glow",
                "objective": "ps-mob.config" },
            "color": "red" },
        { "text": "\n" },
        { "text": "Beam on spawn ",
            "clickEvent": {
                "action": "suggest_command",
                "value": "/scoreboard players set .beam_on_spawn ps-mob.config " },
            "hoverEvent": {
                "action": "show_text",
                "contents": "If Mob Captains should create a particle beam when spawning that shows their location for about 1 second, with 1 being turned on and 0 it being turned off" }},
        { "text": "[0 | 1]", "color": "gray" },
        { "text": " " },
        { "score": {
                "name": ".beam_on_spawn",
                "objective": "ps-mob.config" },
            "color": "red" },
        { "text": "\n" },
        { "text": "Weight common ",
            "clickEvent": {
                "action": "suggest_command",
                "value": "/scoreboard players set .weight_common ps-mob.config " },
            "hoverEvent": {
                "action": "show_text",
                "contents": "Weight for a Mob Captain being of common rarity" }},
        { "text": "[0 | 1]", "color": "gray" },
        { "text": " " },
        { "score": {
                "name": ".weight_common",
                "objective": "ps-mob.config" },
            "color": "red" },
        { "text": "\n" },
        { "text": "Weight uncommon ",
            "clickEvent": {
                "action": "suggest_command",
                "value": "/scoreboard players set .weight_uncommon ps-mob.config " },
            "hoverEvent": {
                "action": "show_text",
                "contents": "Weight for a Mob Captain being of uncommon rarity" }},
        { "text": "[0 | 1]", "color": "gray" },
        { "text": " " },
        { "score": {
                "name": ".weight_uncommon",
                "objective": "ps-mob.config" },
            "color": "red" },
        { "text": "\n" },
        { "text": "Weight rare ",
            "clickEvent": {
                "action": "suggest_command",
                "value": "/scoreboard players set .weight_rare ps-mob.config " },
            "hoverEvent": {
                "action": "show_text",
                "contents": "Weight for a Mob Captain being of rare rarity" }},
        { "text": "[0 | 1]", "color": "gray" },
        { "text": " " },
        { "score": {
                "name": ".weight_rare",
                "objective": "ps-mob.config" },
            "color": "red" },
        { "text": "\n" },
        { "text": "Weight legendary ",
            "clickEvent": {
                "action": "suggest_legendary",
                "value": "/scoreboard players set .weight_legendary ps-mob.config " },
            "hoverEvent": {
                "action": "show_text",
                "contents": "Weight for a Mob Captain being of legendary rarity" }},
        { "text": "[0 | 1]", "color": "gray" },
        { "text": " " },
        { "score": {
                "name": ".weight_legendary",
                "objective": "ps-mob.config" },
            "color": "red" },
        { "text": "\n" }
    ]
